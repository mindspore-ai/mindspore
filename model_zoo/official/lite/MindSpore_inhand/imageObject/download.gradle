/**
 * To download necessary library from HuaWei server.
 * Including mindspore-lite .so file, minddata-lite .so file and model file.
 * The libraries can be downloaded manually.
 */
def targetMindSporeInclude = "src/main/cpp/"
def mindsporeLite_Version = "mindspore-lite-1.1.0-inference-android"
def mindsporeLite_Version_AAR = "mindspore-lite-maven-1.1.0"

def targetModelFile = "src/main/assets/model/mobilenetv2.ms"
def mindSporeLibrary_arm64 = "src/main/cpp/${mindsporeLite_Version}.tar.gz"

def modelDownloadUrl = "https://download.mindspore.cn/model_zoo/official/lite/mobilenetv2_imagenet/mobilenetv2.ms"
def mindsporeLiteDownloadUrl = "https://ms-release.obs.cn-north-4.myhuaweicloud.com/1.1.0/MindSpore/lite/release/android/${mindsporeLite_Version}.tar.gz"

def mindSporeLibraryAAR = "src/main/cpp/${mindsporeLite_Version}/${mindsporeLite_Version_AAR}.zip"
def cleantargetMindSporeIncludeAAR = "../mindsporelibrary"
def targetMindSporeIncludeAAR = "../mindsporelibrary/"

def targetObjectModelFile = "src/main/assets/model/ssd.ms"
def targetGarbageModelFile = "src/main/assets/model/garbage_mobilenetv2.ms"

def modelObjectDownloadUrl = "https://download.mindspore.cn/model_zoo/official/lite/ssd_mobilenetv2_lite/ssd.ms"
def modelGarbageDownloadUrl = "https://download.mindspore.cn/model_zoo/official/lite/garbage_mobilenetv2_lite/garbage_mobilenetv2.ms"

def cleantargetMindSporeInclude = "src/main/cpp"

task cleanCmakeCache(type: Delete) {
    delete '.cxx/cmake/debug'
    delete '.cxx/cmake/release'
}

task downloadModelFile(type: DownloadUrlTask) {
    doFirst {
        println "Downloading ${modelDownloadUrl}"
    }
    sourceUrl = "${modelDownloadUrl}"
    target = file("${targetModelFile}")
}

task downloadObjectModelFile(type: DownloadUrlTask) {
    doFirst {
        println "Downloading ${modelObjectDownloadUrl}"
    }
    sourceUrl = "${modelObjectDownloadUrl}"
    target = file("${targetObjectModelFile}")
}

task downloadGarbageModelFile(type: DownloadUrlTask) {
    doFirst {
        println "Downloading ${modelGarbageDownloadUrl}"
    }
    sourceUrl = "${modelGarbageDownloadUrl}"
    target = file("${targetGarbageModelFile}")
}

task downloadMindSporeLibrary(type: DownloadUrlTask) {
    doFirst {
        println "Downloading ${mindsporeLiteDownloadUrl}"
    }
    sourceUrl = "${mindsporeLiteDownloadUrl}"
    target = file("${mindSporeLibrary_arm64}")
}

task unzipMindSporeInclude(type: Copy, dependsOn: 'downloadMindSporeLibrary') {
    doFirst {
        println "Unzipping ${mindSporeLibrary_arm64}"
    }
    from tarTree(resources.gzip("${mindSporeLibrary_arm64}"))
    into "${targetMindSporeInclude}"
}

task cleanUnusedmindsporeFiles(type: Delete, dependsOn: ['unzipMindSporeInclude']) {
    delete fileTree("${targetMindSporeInclude}").matching {
        include "*.tar.gz"
    }
}

task unzipMindSporeIncludeAAR(type: Copy, dependsOn: ['cleanUnusedmindsporeFiles']) {
    doFirst {
        println "Unzipping ${mindSporeLibraryAAR}"
    }
    from zipTree("${mindSporeLibraryAAR}")
    into "${targetMindSporeIncludeAAR}"
}

task cleanUnusedmindsporeAARFiles(type: Delete, dependsOn: ['unzipMindSporeIncludeAAR']) {
    delete fileTree("${targetMindSporeInclude}").matching {
        include "*.zip"
    }
    delete fileTree("${cleantargetMindSporeIncludeAAR}").matching {
        include "*.zip"
    }
}
/*
 * Using preBuild to download mindspore library and model file.
 * Run before gradle build.
 */
if (file("src/main/cpp/${mindsporeLite_Version}/lib/aarch64/libmindspore-lite.so").exists()) {
    downloadMindSporeLibrary.enabled = false
    unzipMindSporeInclude.enabled = false
    cleanUnusedmindsporeFiles.enabled = false
}

if (file("src/main/assets/model/garbage_mobilenetv2.ms").exists()) {
    downloadGarbageModelFile.enabled = false
}

if (file("src/main/assets/model/mobilenetv2.ms").exists()) {
    downloadModelFile.enabled = false
}

if (file("src/main/assets/model/ssd.ms").exists()) {
    downloadObjectModelFile.enabled = false
}

if (file("../mindsporelibrary/mindspore/mindspore-lite/1.1.0/mindspore-lite-1.1.0.aar").exists()) {
    downloadMindSporeLibrary.enabled = false
    unzipMindSporeIncludeAAR.enabled = false
    cleanUnusedmindsporeAARFiles.enabled = false
}

preBuild.dependsOn cleanCmakeCache
preBuild.dependsOn downloadModelFile
preBuild.dependsOn downloadObjectModelFile
preBuild.dependsOn downloadMindSporeLibrary
preBuild.dependsOn downloadGarbageModelFile
preBuild.dependsOn unzipMindSporeInclude
preBuild.dependsOn unzipMindSporeIncludeAAR
preBuild.dependsOn cleanUnusedmindsporeFiles
preBuild.dependsOn cleanUnusedmindsporeAARFiles

class DownloadUrlTask extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}